plugins {
    id "org.springframework.boot" version "2.1.4.RELEASE"
    id 'application'
    id 'java'
    id 'org.hidetake.swagger.generator' version '2.18.1'
}

repositories {
    jcenter()
}

configurations {
    compile.exclude module: 'spring-boot-starter-tomcat'
}

dependencies {
    // for type inference
    annotationProcessor 'org.projectlombok:lombok:1.18.6'
    compileOnly 'org.projectlombok:lombok:1.18.6'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.6'
    testCompileOnly 'org.projectlombok:lombok:1.18.6'

    // for logging
    compile group: 'ch.qos.logback', name: 'logback-classic', version: '1.2.3'
    compile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.25'

    // dependencies by requiring swagger-codegen generated code
    compile group: 'com.github.joschi.jackson', name: 'jackson-datatype-threetenbp', version: '2.6.4'
    compile group: 'io.springfox', name: 'springfox-swagger-common', version: '2.9.2'
    compile group: 'io.springfox', name: 'springfox-swagger2', version: '2.9.2'
    compile group: 'io.swagger.core.v3', name: 'swagger-annotations', version: '2.0.8'
    compile group: 'org.threeten', name: 'threetenbp', version: '1.3.8'

    // spring boot with jetty
    compile group: 'org.springframework.boot', name: 'spring-boot-starter-jetty', version: '2.1.4.RELEASE'
    compile group: 'org.springframework.boot', name: 'spring-boot-starter-security', version: '2.1.4.RELEASE'
    compile group: 'org.springframework.boot', name: 'spring-boot-starter-web', version: '2.1.4.RELEASE'

    // swagger codegen v3
    swaggerCodegen 'io.swagger.codegen.v3:swagger-codegen-cli:3.0.8'

    implementation 'com.google.guava:guava:27.0.1-jre'
    testImplementation 'junit:junit:4.12'
}

swaggerSources {
    openapi {
        inputFile = file('spec/openapi.yaml')
        code {
            language = 'spring'
            configFile = file('spec/config.json')
            // dependsOn validation
        }
    }
}

compileJava.dependsOn swaggerSources.openapi.code
sourceSets.main.java.srcDir "${swaggerSources.openapi.code.outputDir}/src/main/java"
sourceSets.main.resources.srcDir "${swaggerSources.openapi.code.outputDir}/src/main/resources"

sourceCompatibility = 1.8
targetCompatibility = 1.8

mainClassName = 'com.example.springboot.ServerMain'

// for debugging
tasks.withType(JavaExec) {
    if (System.getProperty('DEBUG', 'false') == 'true') {
        jvmArgs '-Xdebug', '-Xrunjdwp:transport=dt_socket,server=y,suspend=y,address=5005'
    }
}
